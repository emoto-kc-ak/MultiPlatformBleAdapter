buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
    }
}

plugins {
    id 'com.android.library'
    id 'com.palantir.git-version' version '0.12.3'
    id 'maven-publish'
}

group = 'com.github.emoto-kc-ak'

// https://qiita.com/CUTBOSS/items/b4f180d4824fdd50e91a
def repo = new File(rootDir, 'repository')

def artifactPrefix = 'MultiPlatformBleAdapter'

def versionInfo = versionDetails()

repositories {
    google()
    jcenter()
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        profile {
            matchingFallbacks = ['debug', 'release']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.polidea.rxandroidble:rxandroidble:1.7.1"
    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                // https://qiita.com/CUTBOSS/items/b4f180d4824fdd50e91a
                url = "file://${repo.absolutePath}"
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                groupId = group
                artifactId = artifactPrefix
                version = versionInfo.gitHashFull
            }
            debug(MavenPublication) {
                from components.debug

                groupId = group
                artifactId = "${artifactPrefix}-debug"
                version = versionInfo.gitHashFull
            }
        }
    }
}
